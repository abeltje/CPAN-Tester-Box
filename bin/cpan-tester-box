#! /usr/bin/perl -w
use strict;
use v5.20.0;
use if $] <  5.036, experimental => 'signatures';
use if $] >= 5.036, feature      => 'signatures';

use CPAN::Tester::RecentUploads;
use CPAN::Tester::Box;
use File::Basename qw< basename >;
use Pod::Usage;
use YAML::XS qw< DumpFile LoadFile >;

use Getopt::Long;
my %option = (
    interval => 60 * 60,                     # seconds
    perl     => $^X,
    db       => basename($0) . '.db',
    verbose  => 1,
    help     => sub {
        pod2usage(exit_value => 0, verbose => 1);
    },
);
set_default_options(\%option, basename($0) . ".yml");

GetOptions(
    \%option => qw<
        perl|p=s
        interval|i=i
        verbose|v:1
        quiet|q
        help|h
    >
);
$option{verbose} = 0 if $option{quiet};

my $db = { };
if (-e $option{db}) {
    eval { $db = LoadFile($option{db}); 1; }
        or die "LoadFile($option{db}): $@";
}
my $recents = CPAN::Tester::RecentUploads->new(interval => '1d');
my $box = CPAN::Tester::Box->new(
    recent_uploads => $recents,
    tester_perl    => $option{perl},
    handled        => $db,
    poll_interval  => $option{interval},
    verbose        => $option{verbose},
);
{
    local $SIG{INT} = local $SIG{TERM} = sub { exit(42) };
    $box->run();
}

END {
    if (defined($box)) {
        say "Saving database...";
        DumpFile($option{db}, $box->handled);
    }
}

sub set_default_options ($options, $filename) {
    my $opt_from_file = { };
    if (-e $filename) {
        eval { $opt_from_file = LoadFile($filename); 1; }
            or die "Cannot unyaml($filename): $@";
    }
    for my $key (keys %$opt_from_file) {
        $options->{$key} = $opt_from_file->{$key};
    }
}

=head1 NAME

cpan-tester-box - A simple CPAN tester box

=head2 SYNOPSIS

    cpan-tester-box [options]

or

    nohup cpan-tester-box [options] &

=head1 OPTIONS

  cpan-tester-box [options]

  --interval|-i <seconds>           default 3600 (1 hour)
  --perl| -p <path-to-tester-perl>  default $^X  (current perl)
  --verbose|-v <0 | 1 | 2>          default 1 (lone -v, sets to 1)
  --quiet|-q                        force verbose to 0

Defaults will be changed from F<cpan-tester-box.yml>.

=head1 DESCRIPTION

    $ nohup cpan-tester-box &
    $ tail -f nohup.out

=head2 Config-file

    cpan-tester-box.yml

=head1 COPYRIGHT

E<copy> MMXXIII - Abe Timmerman <abeltje@cpan.org>

=head1 LICENSE

This library is free software; you can redistribute it and/or modify
it under the same terms as Perl itself.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.

=cut
